
proc buildAttrTree(string $nodeName, string $attr)
{
    if ($attr == "input")
        return;

    string $fullAttrName = $nodeName + "." + $attr;
    string $type = `getAttr -type $fullAttrName`;
    string $children[] = `attributeQuery -n $nodeName -lc $attr`;
    string $niceName = `attributeQuery -n $nodeName -nn $attr`;

    if ("TdataCompound" != $type)
    {
		if ($type == "" )
		{
			editorTemplate -suppress $attr;
		}
        else if ("Separator" == $niceName)
        {
            editorTemplate -addSeparator;
        } else
        {
            editorTemplate -addDynamicControl $attr;
        }
        return;
    }

    editorTemplate -beginLayout $niceName -collapse true;
    for ($i=0; $i<size($children); $i++)
    {
        string $child = $children[$i];
        buildAttrTree($nodeName, $child);
    }
    editorTemplate -endLayout;
}


global proc AEhoudiniAssetSetInputToSelection(string $textField, string $inputAttr)
{
    string $buf[] = `ls -sl`;

    if (size($buf) == 1)
    {
        if (`objExists $buf[0]` && (`nodeType $buf[0]` == "mesh" || 
                                    `nodeType $buf[0]` == "houdiniAsset") )
        {
            AEhoudiniAssetSetInput($textField, $inputAttr, $buf[0]);
            return;
        }
    }

    string $buf2[] = `listRelatives -ad $buf`;
    string $sel[] = `ls -typ mesh $buf2`;
    string $meshes[] = `ls -sl -typ mesh`;
 
    if (size($sel) == 1)
        AEhoudiniAssetSetInput($textField, $inputAttr, $sel[0]);
}


global proc AEhoudiniAssetSetInputToText(string $textField, string $inputAttr)
{    
    string $str = `textField -q -text $textField`;

    if (!`objExists $str`)
    {
        AEhoudiniAssetSetInput($textField, $inputAttr, $str);
        return;
    }

    if (`nodeType $str` == "mesh" || `nodeType $str` == "houdiniAsset")
    {
        AEhoudiniAssetSetInput($textField, $inputAttr, $str);
        return;
    }

    string $buf2[] = `listRelatives -ad $str`;
    string $sel[] = `ls -typ mesh $buf2`;
    if (size($sel) == 1)
    {
        AEhoudiniAssetSetInput($textField, $inputAttr, $sel[0]);
        return;
    }

    
}


global proc AEhoudiniAssetSetInput(string $textField, string $inputAttr, string $str)
{
    string $inputTransformAttr = $inputAttr + "Transform";
    if ($str == "")
    {
        // remove input if cleared
        string $inPlug = `connectionInfo -sfd $inputAttr`;
        string $inTransformPlug = `connectionInfo -sfd $inputTransformAttr`;
        if ($inPlug != "")
        {
            disconnectAttr $inPlug $inputAttr;
            disconnectAttr $inTransformPlug $inputTransformAttr;
            //string $inMesh = plugNode($inPlug);
            //showHidden $inMesh;
        }
    } else
    {       
        
        // connect new input
        if (`objExists $str`)
	{            
            if(`nodeType $str` == "houdiniAsset")
            {                
                //If they just type the name of a houdini asset, we'll hook it up assuming the 1st output
                string $outputMeshPlug = $str + ".output.outputObjects[0].outputObjectMesh";
                string $outputMeshConn[] = `listConnections -s 0 -d 1 $outputMeshPlug`;
                if( size($outputMeshConn) > 0 )
                {
                    connectAttr -force ($str + ".output.outputObjects[0].outputObjectMetaData") $inputAttr;
                    connectAttr -force ($str + ".wm[0]") $inputTransformAttr;
                }
                else
                {
                    error("Couldn't find mesh output on the asset.\n");
                }
            }
            else if (`nodeType $str` == "mesh")
            {

	        string $histPlug = `connectionInfo -sfd ($str + ".inMesh")`;
	        string $histNode = plugNode($histPlug);
            
	        if ($histNode != ""
		        && `nodeType $histNode` == "houdiniAsset")
	        {
		    // TODO: Assume that assets with inputs are SOPs
		    // *** A bit of a hack, plugNode just takes a substring of 
		    // everything before the first '.', even if the substring is
		    // not a node.
		    string $obj = plugNode(plugAttr($histPlug));                    

		    connectAttr -force ($histNode + "." + $obj + ".outputObjectMetaData") $inputAttr;
                    connectAttr -force ($histNode + ".wm[0]") $inputTransformAttr;
	        }
	        else 
	        {
		    connectAttr -force ($str + ".outMesh") $inputAttr;
                    string $parent[] = `listRelatives -parent $str`;
                    if( size($parent) > 0 && `nodeType $parent[0]` == "transform" )
                    {
                        string $parentWorldMatrixPlug = $parent[0] + ".wm[0]";
                        connectAttr -force $parentWorldMatrixPlug $inputTransformAttr;
                    }
		    //hide $str;
	        }            
            }
            else
            {
                error("Please input the name of a mesh or houdini asset.\n");
            }
	}
        
    }

    // set the appropriate text
    string $connectedMeshAttr = `connectionInfo -sfd $inputAttr`;
    string $connectedMesh = plugNode($connectedMeshAttr);
    textField -edit -text $connectedMesh $textField;
}


global proc AEhoudiniAssetInputsNew(string $attrFullName)
{

    string $buffer[];
    tokenize($attrFullName, ".", $buffer);
    string $nodeName = $buffer[0];
    string $attrName = $buffer[1];

    if( `attributeExists $attrName $nodeName` == 0)
    {
        print("Warning: nodename= " + $nodeName + " attrName=" + $attrName + " Not found in AEhoudiniAssetInputsNew\n");
        return;
    }

    string $children[] = `attributeQuery -n $nodeName -lc $attrName`;

    //editorTemplate -beginLayout "Inputs" -collapse false;
    columnLayout -adj true "inputsLayout";
    for ($i=0; $i<size($children); $i++)
    {
        string $child = $children[$i];
        string $niceName = `attributeQuery -n $nodeName -nn $child`;


        rowLayout -numberOfColumns 3
            -columnWidth3 50 150 50
            -adjustableColumn 2
            -parent "inputsLayout"
            -columnAlign  1 "right"
            -columnAttach 1 "both"     2
            -columnAttach 2 "both"  2
            -columnAttach 3 "left"  2;
            text -label $niceName;
            string $inputTextField = `textField -aie true`;
            AEhoudiniAssetSetInput $inputTextField ($nodeName + "." + $child) " ";
            textField -edit -enterCommand
                ("AEhoudiniAssetSetInputToText \"" + $inputTextField + "\" \"" 
                + ($nodeName + "." + $child) + "\"") $inputTextField;
            button -label "Set to Selection" 
                -c ("AEhoudiniAssetSetInputToSelection \"" + $inputTextField + "\" \"" 
                + ($nodeName + "." + $child) + "\"");
    }

}


global proc AEhoudiniAssetInputsReplace(string $attrFullName)
{
    deleteUI "inputsLayout";
    AEhoudiniAssetInputsNew($attrFullName);
}


global proc AEhoudiniAssetTemplate( string $nodeName )
{
    
    editorTemplate -beginScrollLayout;
    editorTemplate -addExtraControls;
    

    editorTemplate -addControl "assetPath";

    // special ui for inputs
    string $inputAttrArray[] = `listAttr -st "inputs" $nodeName`;
    string $inputAttr = $inputAttrArray[0];
    if ($inputAttr != "")
    {
        editorTemplate -beginLayout "Inputs" -collapse false;
        editorTemplate -callCustom "AEhoudiniAssetInputsNew" "AEhoudiniAssetInputsReplace"
            $inputAttr;
       
        editorTemplate -endLayout;
    }


    // get list of attributes
    string $attrs[] = `listAttr -ud $nodeName`;

    for ($i=0; $i<size($attrs); $i++)
    {
        string $attr = $attrs[$i];
        string $parents[] = `attributeQuery -n $nodeName -lp $attr`;
        if ((0 == size($parents)) && $attr != "inputs")
        {
            buildAttrTree($nodeName, $attr);
        }
        editorTemplate -suppress $attr;
    }

    editorTemplate -endLayout;

    editorTemplate -suppress "caching";
    editorTemplate -suppress "nodeState";



}

