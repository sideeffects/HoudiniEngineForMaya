proc hapiPopulateTree(string $uiTree)
{
	string $otl_scan_path = `getenv "HAPI_OTL_PATH"`;
	string $array[];
	$array = stringToStringArray($otl_scan_path, "/");

	string $otl_path = "";
	for($ii = 0; $ii < size($array) - 1; $ii++)
	{
		$otl_path = $otl_path + $array[$ii] + "/";
	}

	string $logoPath = $otl_path + "sideEffectsLogo.png";

	string $otlDirContents[] = `getFileList -folder $otl_path`;

	for($ii = 0; $ii < size($otlDirContents) ; $ii++)
	{
		string $subDir = $otl_path + $otlDirContents[$ii] + "/";

		string $subDirContents[] = `getFileList -folder $subDir -filespec "*.*"`;

		for($jj = 0; $jj < size($subDirContents) ; $jj++)
		{
			string $fullPath = $subDir + $subDirContents[$jj];
			string $cmd = "houdiniAsset -loadAsset \"" + $fullPath + "\"";
			string $filePath = "All Assets/" + $otlDirContents[$ii] + "/" + $subDirContents[$jj];
			treeLister -e -add $filePath $logoPath $cmd
				  $uiTree;
		}

	}
}

global proc hapiAboutBox()
{
	string $aboutBox = `window -width 200 -t "About HAPI"`;
	string $otl_scan_path = `getenv "HAPI_OTL_PATH"`;
	string $hapi_path = `getenv "HAPI_PATH"`;
	string $otl_scan_path_label = "HAPI_OTL_PATH: " + $otl_scan_path;
	string $hapi_path_label = "HAPI_PATH: " + $hapi_path;

	string $closeCmd = "deleteUI " + $aboutBox;
	columnLayout -adjustableColumn true;
		text -label $otl_scan_path_label;
		text -label $hapi_path_label;
		button -label "OK" -c $closeCmd;

	showWindow;
}

global proc hapiInstanceAllCoreAssets()
{
	string $otl_scan_path = `getenv "HAPI_OTL_PATH"`;
	string $array[];
	$array = stringToStringArray($otl_scan_path, "/");

	string $otl_path = "";
	for($ii = 0; $ii < size($array) - 1; $ii++)
	{
		$otl_path = $otl_path + $array[$ii] + "/";
	}

	string $subDir = $otl_path + "Core/";

	string $subDirContents[] = `getFileList -folder $subDir -filespec "*.*"`;

	for($jj = 0; $jj < size($subDirContents) ; $jj++)
	{
		string $fullPath = $subDir + $subDirContents[$jj];
		string $cmd = "houdiniAsset -loadAsset \"" + $fullPath + "\"";
		eval $cmd;
	}
}

global proc saveHipFile()
{
	string $filter = "Houdini Scene Files (*.hip);;All Files (*.*)";
    string $results[] = `fileDialog2 -cap "Save Houdini Asset" -okc "Save" -fileFilter $filter -fm 0`;
    if(size($results) > 0)
        houdiniAsset -saveHIP $results[0];
}

global proc hapiDebugWindow()
{
	window -menuBar true -width 500 -height 600 -t "HAPI Debug";

	menu -label "File" -tearOff true;
    menuItem -label "Save Current Houdini Scene..." -c "saveHipFile";
	menuItem -label "Load Houdini Asset..." -c "houdiniAsset_loadAssetMenuClick";
	menu -label "Help" -helpMenu true;
		menuItem -label "About..." -c "hapiAboutBox";

	formLayout "hapiForm";
	string $instanceAllCoreButton = `button -label "Instantiate All Core Assets" -c "hapiInstanceAllCoreAssets"`;
	string $uiTree = `treeLister`;

	hapiPopulateTree($uiTree);

	formLayout -e
		//-af $instanceAllCoreButton top 0
		-af $instanceAllCoreButton left 0
		-af $instanceAllCoreButton right 0
		-ac $instanceAllCoreButton "bottom" 0 $uiTree
		-af $uiTree top 0
		-af $uiTree left 0
		-af $uiTree bottom 0
		-af $uiTree right 0
		hapiForm;
	showWindow;
}

