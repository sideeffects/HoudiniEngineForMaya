global string $houdiniEngineLoadAsset_loadedOTLFilePaths[];

global proc
houdiniEngineLoadAsset_browseOTL()
{
    string $filter = "Houdini Digital Assets (*.otl);;All Files (*.*)";
    string $results[] = `fileDialog2
        -caption "Load Houdini Asset"
        -okCaption "Open"
        -fileFilter $filter
        -fileMode 1`;
    if (size($results) == 0)
    {
        return;
    }

    string $otlFilePath = $results[0];

    houdiniEngineLoadAsset_loadOTL($otlFilePath);
}

global proc
houdiniEngineLoadAsset_loadOTL(string $otlFilePath)
{
    global string $houdiniEngineLoadAsset_loadedOTLFilePaths[];

    if(size(`houdiniAsset -listAssets $otlFilePath`) == 0)
    {
        return;
    }

    houdiniEngineLoadAsset_addRecentOTLFile $otlFilePath;

    // Maya 2012 doesn't have stringArrayFind
    int $index = -1;
    for($i = 0; $i < size($houdiniEngineLoadAsset_loadedOTLFilePaths); $i++)
    {
        if($otlFilePath == $houdiniEngineLoadAsset_loadedOTLFilePaths[$i])
        {
            $index = $i;
            break;
        }
    }

    // does it already exist?
    if($index == -1)
    {
        $houdiniEngineLoadAsset_loadedOTLFilePaths[size($houdiniEngineLoadAsset_loadedOTLFilePaths)] =
            $otlFilePath;
        $houdiniEngineLoadAsset_loadedOTLFilePaths = `sort $houdiniEngineLoadAsset_loadedOTLFilePaths`;
    }

    houdiniEngineLoadAsset_refreshAssetList;
}

global proc
houdiniEngineLoadAsset_addRecentOTLFile(string $otlFilePath)
{
    int $index = -1;
    if(`optionVar -exists houdiniEngine_recentOTLFilePaths`)
    {
        // Maya 2012 doesn't have stringArrayFind
        string $recentOTLFilePaths[] = `optionVar -q houdiniEngine_recentOTLFilePaths`;
        for($i = 0; $i < size($recentOTLFilePaths); $i++)
        {
            if($otlFilePath == $recentOTLFilePaths[$i])
            {
                $index = $i;
                break;
            }
        }
    }

    // does it already exist?
    if($index != -1)
    {
        optionVar -removeFromArray houdiniEngine_recentOTLFilePaths $index;
        optionVar -stringValueAppend houdiniEngine_recentOTLFilePaths $otlFilePath;
    }
    else
    {
        optionVar -stringValueAppend houdiniEngine_recentOTLFilePaths $otlFilePath;
    }

    int $numToRemove = `optionVar -arraySize houdiniEngine_recentOTLFilePaths` - 20;
    for($i = 0; $i < $numToRemove; $i++)
    {
        optionVar -removeFromArray houdiniEngine_recentOTLFilePaths 0;
    }
}

global proc
houdiniEngineLoadAsset_refreshAssetList()
{
    global string $houdiniEngineLoadAsset_loadedOTLFilePaths[];

    if(!`window -exists houdiniEngineLoadAssetWindow`)
    {
        return;
    }

    treeView -edit -removeAll houdiniEngineAssetTreeView;

    for($otlFilePath in $houdiniEngineLoadAsset_loadedOTLFilePaths)
    {
        treeView -e
            -addItem $otlFilePath ""
            houdiniEngineAssetTreeView;

        for($asset in `houdiniAsset -listAssets $otlFilePath`)
        {
            string $command = "houdiniAsset -loadAsset \"" + encodeString($otlFilePath) + "\""
                + " \"" + $asset + "\"";

            string $itemName = ($otlFilePath + "," + $asset);
            treeView -e
                -addItem $itemName $otlFilePath
                houdiniEngineAssetTreeView;
            treeView -e
                -displayLabel $itemName $asset
                houdiniEngineAssetTreeView;
        }
    }
}

global proc
houdiniEngineLoadAsset_buildRecentOTLsMenu(string $menuItem)
{
    string $recentOTLFilePaths[];
    if(`optionVar -exists houdiniEngine_recentOTLFilePaths`)
    {
        $recentOTLFilePaths = `optionVar -q houdiniEngine_recentOTLFilePaths`;
    }

    // same as existing menuItems?
    string $existingMenus[] = `menu -query -itemArray $menuItem`;
    int $numExistingMenus = size($existingMenus);
    if ($numExistingMenus == size($recentOTLFilePaths))
    {
        for ($i = 0; $i < $numExistingMenus; $i++)
        {
            string $label = `menuItem -query -label $existingMenus[$i]`;
            if ($label != $recentOTLFilePaths[$numExistingMenus-$i-1])
            {
                break;
            }
        }

        if ($i == $numExistingMenus)
        {
            return;
        }
    }

    // clear existing
    menu -edit -deleteAllItems $menuItem;

    setParent -menu $menuItem;

    for($i = size($recentOTLFilePaths); $i-- > 0;)
    {
        menuItem
            -label $recentOTLFilePaths[$i]
            -command ("houdiniEngineLoadAsset_loadOTL"
                    + " \"" + encodeString($recentOTLFilePaths[$i]) + "\"");
    }
}

global proc
houdiniEngineLoadAsset_loadAsset(string $item)
{
    // if the user double clicked on the OTL file, do nothing
    // check -itemExists because of treeView bug with displayLabel
    if(`treeView -q -itemExists $item houdiniEngineAssetTreeView`
            && !`treeView -q -isLeaf $item houdiniEngineAssetTreeView`)
    {
        return;
    }

    //treeView has a bug where the itemDblClickCommand is called with the
    //displayLabel instead of the item name, so we can't use comma as a
    //delimiter.

    //string $tokens[];
    //tokenize $item "," $tokens;

    //string $otlFilePath = $tokens[0];
    //string $asset = $tokens[1];

    string $otlFilePath;
    string $asset;

    // workaround the treeView bug
    // check the selected item
    if(size($asset) == 0)
    {
        string $selectItem[] = `treeView -q -selectItem houdiniEngineAssetTreeView`;

        if(size($selectItem) == 1)
        {
            string $tokens[];
            tokenize $selectItem[0] "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $otlFilePath = $tokens[0];
                $asset = $tokens[1];
            }
        }
    }

    // workaround the treeView bug
    // find it from the list of children
    if(size($asset) == 0)
    {
        string $child;
        for($child in `treeView -q -children "" houdiniEngineAssetTreeView`)
        {
            string $tokens[];
            tokenize $child "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $otlFilePath = $tokens[0];
                $asset = $tokens[1];

                break;
            }
        }
    }

    if(size($asset) == 0)
    {
        error "Cannot determine clicked item from treeView due to Maya bug.";
        return;
    }

    houdiniAsset -loadAsset $otlFilePath $asset;
}

global proc
houdiniEngineLoadAsset()
{
    if(`window -exists houdiniEngineLoadAssetWindow`)
    {
        showWindow houdiniEngineLoadAssetWindow;
        return;
    }

    window
        -title "Houdini Asset Manager"
        -menuBar true
        houdiniEngineLoadAssetWindow;

    // controls
    string $formLayout = `formLayout`;
    // Maya 2012 and 2013 don't support "-allowMultiSelection"
    string $treeView = `treeView houdiniEngineAssetTreeView`;

    formLayout -e
        -attachForm houdiniEngineAssetTreeView top 0
        -attachForm houdiniEngineAssetTreeView left 0
        -attachForm houdiniEngineAssetTreeView bottom 0
        -attachForm houdiniEngineAssetTreeView right 0
        $formLayout;

    treeView -e
        -itemDblClickCommand ("houdiniEngineLoadAsset_loadAsset")
        houdiniEngineAssetTreeView;

    // menus
    menu -label "File";
        menuItem
            -label "Browse OTL File..."
            -command "houdiniEngineLoadAsset_browseOTL";
        $recentOTLsMenu = `menuItem
            -label "Recent OTL Files"
            -subMenu true`;
        setParent -menu ..;

        menuItem -divider true;

        menuItem
            -label "Close"
            -command ("evalDeferred \"deleteUI -window houdiniEngineLoadAssetWindow\"");

    menuItem -e -postMenuCommand ("houdiniEngineLoadAsset_buildRecentOTLsMenu"
            + " \"" + $recentOTLsMenu + "\"")
        $recentOTLsMenu;

    // populate asset list
    houdiniEngineLoadAsset_refreshAssetList;

    showWindow;
}
