global string $houdiniEngineOTLsManager_loadedOTLFilePaths[];

global proc
houdiniEngineOTLsManager_addLoadedFile(string $assetLibraryFilePath)
{
    global string $houdiniEngineOTLsManager_loadedOTLFilePaths[];

    // Maya 2012 doesn't have stringArrayFind
    int $index = -1;
    for($i = 0; $i < size($houdiniEngineOTLsManager_loadedOTLFilePaths); $i++)
    {
        if($assetLibraryFilePath == $houdiniEngineOTLsManager_loadedOTLFilePaths[$i])
        {
            $index = $i;
            break;
        }
    }

    // does it already exist?
    if($index == -1)
    {
        $houdiniEngineOTLsManager_loadedOTLFilePaths[size($houdiniEngineOTLsManager_loadedOTLFilePaths)] =
            $assetLibraryFilePath;
        $houdiniEngineOTLsManager_loadedOTLFilePaths = `sort $houdiniEngineOTLsManager_loadedOTLFilePaths`;
    }

}

global proc
houdiniEngineOTLsManager_refreshAssetList()
{
    global string $houdiniEngineOTLsManager_loadedOTLFilePaths[];

    if(!`window -exists houdiniEngineOTLsManagerWindow`)
    {
        return;
    }

    treeView -edit -removeAll houdiniEngineAssetTreeView;

    for($otlFilePath in $houdiniEngineOTLsManager_loadedOTLFilePaths)
    {
        treeView -e
            -addItem $otlFilePath ""
            houdiniEngineAssetTreeView;

        for($asset in `houdiniAsset -listAssets $otlFilePath`)
        {
            string $command = "houdiniAsset -loadAsset \"" + encodeString($otlFilePath) + "\""
                + " \"" + $asset + "\"";

            string $itemName = ($otlFilePath + "," + $asset);
            treeView -e
                -addItem $itemName $otlFilePath
                houdiniEngineAssetTreeView;
            treeView -e
                -displayLabel $itemName $asset
                houdiniEngineAssetTreeView;
        }
    }
}

global proc
houdiniEngineOTLsManager_loadAsset(string $item)
{
    // if the user double clicked on the OTL file, do nothing
    // check -itemExists because of treeView bug with displayLabel
    if(`treeView -q -itemExists $item houdiniEngineAssetTreeView`
            && !`treeView -q -isLeaf $item houdiniEngineAssetTreeView`)
    {
        return;
    }

    //treeView has a bug where the itemDblClickCommand is called with the
    //displayLabel instead of the item name, so we can't use comma as a
    //delimiter.

    //string $tokens[];
    //tokenize $item "," $tokens;

    //string $otlFilePath = $tokens[0];
    //string $asset = $tokens[1];

    string $otlFilePath;
    string $asset;

    // workaround the treeView bug
    // check the selected item
    if(size($asset) == 0)
    {
        string $selectItem[] = `treeView -q -selectItem houdiniEngineAssetTreeView`;

        if(size($selectItem) == 1)
        {
            string $tokens[];
            tokenize $selectItem[0] "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $otlFilePath = $tokens[0];
                $asset = $tokens[1];
            }
        }
    }

    // workaround the treeView bug
    // find it from the list of children
    if(size($asset) == 0)
    {
        string $child;
        for($child in `treeView -q -children "" houdiniEngineAssetTreeView`)
        {
            string $tokens[];
            tokenize $child "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $otlFilePath = $tokens[0];
                $asset = $tokens[1];

                break;
            }
        }
    }

    if(size($asset) == 0)
    {
        error "Cannot determine clicked item from treeView due to Maya bug.";
        return;
    }

    houdiniAsset -loadAsset $otlFilePath $asset;
}

global proc
houdiniEngineOTLsManager()
{
    if(`window -exists houdiniEngineOTLsManagerWindow`)
    {
        showWindow houdiniEngineOTLsManagerWindow;
        return;
    }

    window
        -title "OTLs Manager"
        -menuBar true
        houdiniEngineOTLsManagerWindow;

    // controls
    string $formLayout = `formLayout`;
    // Maya 2012 and 2013 don't support "-allowMultiSelection"
    string $treeView = `treeView houdiniEngineAssetTreeView`;

    formLayout -e
        -attachForm houdiniEngineAssetTreeView top 0
        -attachForm houdiniEngineAssetTreeView left 0
        -attachForm houdiniEngineAssetTreeView bottom 0
        -attachForm houdiniEngineAssetTreeView right 0
        $formLayout;

    treeView -e
        -itemDblClickCommand ("houdiniEngineOTLsManager_loadAsset")
        houdiniEngineAssetTreeView;

    // menus
    menu -label "File";
        menuItem
            -label "Close"
            -command ("evalDeferred \"deleteUI -window houdiniEngineOTLsManagerWindow\"");

    // populate asset list
    houdiniEngineOTLsManager_refreshAssetList;

    showWindow;
}
