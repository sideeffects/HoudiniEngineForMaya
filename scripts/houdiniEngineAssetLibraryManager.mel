global string $houdiniEngineAssetLibraryManager_assetLibraryFilePaths[];

global proc
houdiniEngineAssetLibraryManager_addLoadedFile(string $assetLibraryFilePath)
{
    global string $houdiniEngineAssetLibraryManager_assetLibraryFilePaths[];

    // Maya 2012 doesn't have stringArrayFind
    int $index = -1;
    for($i = 0; $i < size($houdiniEngineAssetLibraryManager_assetLibraryFilePaths); $i++)
    {
        if($assetLibraryFilePath == $houdiniEngineAssetLibraryManager_assetLibraryFilePaths[$i])
        {
            $index = $i;
            break;
        }
    }

    // does it already exist?
    if($index == -1)
    {
        $houdiniEngineAssetLibraryManager_assetLibraryFilePaths[size($houdiniEngineAssetLibraryManager_assetLibraryFilePaths)] =
            $assetLibraryFilePath;
        $houdiniEngineAssetLibraryManager_assetLibraryFilePaths = `sort $houdiniEngineAssetLibraryManager_assetLibraryFilePaths`;
    }

}

global proc
houdiniEngineAssetLibraryManager_refreshAssetList()
{
    global string $houdiniEngineAssetLibraryManager_assetLibraryFilePaths[];

    if(!`window -exists houdiniEngineAssetLibraryManagerWindow`)
    {
        return;
    }

    treeView -edit -removeAll houdiniEngineAssetTreeView;

    for($assetLibraryFilePath in $houdiniEngineAssetLibraryManager_assetLibraryFilePaths)
    {
        treeView -e
            -addItem $assetLibraryFilePath ""
            houdiniEngineAssetTreeView;

        for($asset in `houdiniAsset -listAssets $assetLibraryFilePath`)
        {
            string $command = "houdiniAsset -loadAsset \"" + encodeString($assetLibraryFilePath) + "\""
                + " \"" + $asset + "\"";

            string $itemName = ($assetLibraryFilePath + "," + $asset);
            treeView -e
                -addItem $itemName $assetLibraryFilePath
                houdiniEngineAssetTreeView;
            treeView -e
                -displayLabel $itemName $asset
                houdiniEngineAssetTreeView;
        }
    }
}

global proc
houdiniEngineAssetLibraryManager_loadAsset(string $item)
{
    // if the user double clicked on the OTL file, do nothing
    // check -itemExists because of treeView bug with displayLabel
    if(`treeView -q -itemExists $item houdiniEngineAssetTreeView`
            && !`treeView -q -isLeaf $item houdiniEngineAssetTreeView`)
    {
        return;
    }

    //treeView has a bug where the itemDblClickCommand is called with the
    //displayLabel instead of the item name, so we can't use comma as a
    //delimiter.

    //string $tokens[];
    //tokenize $item "," $tokens;

    //string $assetLibraryFilePath = $tokens[0];
    //string $asset = $tokens[1];

    string $assetLibraryFilePath;
    string $asset;

    // workaround the treeView bug
    // check the selected item
    if(size($asset) == 0)
    {
        string $selectItem[] = `treeView -q -selectItem houdiniEngineAssetTreeView`;

        if(size($selectItem) == 1)
        {
            string $tokens[];
            tokenize $selectItem[0] "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $assetLibraryFilePath = $tokens[0];
                $asset = $tokens[1];
            }
        }
    }

    // workaround the treeView bug
    // find it from the list of children
    if(size($asset) == 0)
    {
        string $child;
        for($child in `treeView -q -children "" houdiniEngineAssetTreeView`)
        {
            string $tokens[];
            tokenize $child "," $tokens;

            if(size($tokens) == 2 && $tokens[1] == $item)
            {
                $assetLibraryFilePath = $tokens[0];
                $asset = $tokens[1];

                break;
            }
        }
    }

    if(size($asset) == 0)
    {
        error "Cannot determine clicked item from treeView due to Maya bug.";
        return;
    }

    houdiniAsset -loadAsset $assetLibraryFilePath $asset;
}

global proc
houdiniEngineAssetLibraryManager()
{
    if(`window -exists houdiniEngineAssetLibraryManagerWindow`)
    {
        showWindow houdiniEngineAssetLibraryManagerWindow;
        return;
    }

    window
        -title "Asset Library Manager"
        -menuBar true
        houdiniEngineAssetLibraryManagerWindow;

    // controls
    string $formLayout = `formLayout`;
    // Maya 2012 and 2013 don't support "-allowMultiSelection"
    string $treeView = `treeView houdiniEngineAssetTreeView`;

    formLayout -e
        -attachForm houdiniEngineAssetTreeView top 0
        -attachForm houdiniEngineAssetTreeView left 0
        -attachForm houdiniEngineAssetTreeView bottom 0
        -attachForm houdiniEngineAssetTreeView right 0
        $formLayout;

    treeView -e
        -itemDblClickCommand ("houdiniEngineAssetLibraryManager_loadAsset")
        houdiniEngineAssetTreeView;

    // menus
    menu -label "File";
        menuItem
            -label "Close"
            -command ("evalDeferred \"deleteUI -window houdiniEngineAssetLibraryManagerWindow\"");

    // populate asset list
    houdiniEngineAssetLibraryManager_refreshAssetList;

    showWindow;
}

